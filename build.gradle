plugins {
    id 'eclipse'
    id 'maven-publish'
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
}

apply plugin: 'java'
apply plugin: 'architectury-plugin'   // Parchment mappings.
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'maven-publish'
apply plugin: 'dev.architectury.loom'

group = 'games.moegirl.sinocraft.sinocalligraphy' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'sinocalligraphy'

version = '1.18.2-2.0.0'
if (System.getenv("JITPACK") == "true") {
    version += "-${System.getenv("VERSION")}"
}

var release = false
if (release || (project.hasProperty("SINOSERIES_RELEASE") && project.property("SINOSERIES_RELEASE") == 'true')) {
    version += '-release'
} else {
    version += '-dev'
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') +
        ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ')' +
        ' Arch: ' + System.getProperty('os.arch'))

println("Building SinoCalligraphy. Ver: $version.")

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    silentMojangMappingsLicense()

    forge {
        dataGen {
            mod 'sinocalligraphy'
        }
    }

    launches {
        data {
            arg "--existing", file("src/main/resources").absolutePath
        }
    }
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

repositories {
    // Put repositories for dependencies here
    // ForgeGradle automatically adds the Forge maven and Maven Central for you

    // If you have mod jar dependencies in ./libs, you can declare them as a repository like so:
    // flatDir {
    //     dir 'libs'
    // }

    maven {
        name = 'Jitpack'
        url = 'https://jitpack.io'
    }

    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }

    // JEI, Hello TT432, THANK YOU!
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }

    // Local maven for TEST.
    maven {
        name = "Local Maven"
        url = "file://${projectDir}/../mavenLocal"
    }
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    minecraft "com.mojang:minecraft:1.18.2"
    forge "net.minecraftforge:forge:1.18.2-40.1.0"

    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.18.2:2022.05.22@zip")
    }

    modCompileOnly('com.github.SinoCraftProject:SinoCore:dev-SNAPSHOT:api') {
        changing = true
    }
    modRuntimeOnly('com.github.SinoCraftProject:SinoCore:dev-SNAPSHOT') {
        changing = true
    }

    modImplementation('com.github.SinoCraftProject:SinoFoundation:dev-SNAPSHOT') {
        changing = true
    }
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    archiveClassifier.set('')

    manifest {
        attributes([
                "Specification-Title"     : "sinocalligraphy",
                "Specification-Vendor"    : "SinoCraft Project Team",
                "Specification-Version"   : "4", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "MoeGirl Games",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

task injectString(type: Copy) {
    from ('src/main/java') {
        include '**/*.java'
        filter(ReplaceTokens, tokens: [version: project.version])
    }
    into "${buildDir}/generated/java"
}

// qyl27: It only works with main.
compileJava.source = "${buildDir}/generated/java"
compileJava.dependsOn injectString
